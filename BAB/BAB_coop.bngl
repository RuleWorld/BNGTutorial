#BAB_coop
# Simple binding model with a bivalent A molecule that has two identical sites 
# for binding of B. Cooperative interactions between B molecules affect the binding 
# constants for binding to the second site.

begin model
  begin parameters
    A0 100  # Initial number of A molecules
    B0 100  # Initial number of B molecules
    ka 0.01 # A-B association rate constant (1/molecule 1/s)
    kd 1    # A-B dissociation rate constant (1/s)
    coop 50 # Enhancement factor for cooperative binding.
  end parameters
  begin molecule types
    A(b,b)
    B(a)
  end molecule types
  begin seed species
    A(b,b) A0
    B(a) B0
  end seed species
  begin observables
    Molecules AB A(b,b!1).B(a!1)
    Species   BAB B(a!1).A(b!1,b!2).B(a!2)
  end observables
  begin reaction rules
    AB_1: A(b,b) + B(a) <-> A(b,b!1).B(a!1) ka, kd
    AB_2: A(b!+,b) + B(a) <-> A(b!+,b!1).B(a!1) ka, kd/coop
  end reaction rules
end model

simulate({method=>"ode",t_end=>10,n_steps=>200})
#parameter_scan({parameter=>"A0",par_min=>1,par_max=>1e4,n_scan_pts=>50,\
#                method=>"ode",t_end=>10000,log_scale=>1})