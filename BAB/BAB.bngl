# Simple binding model with a bivalent A molecule that has two identical sites 
# for binding of B. Binding at each site is not affected by the status of the other
# site (noncooperative binding).
begin model
  begin parameters
    A0 100  # Initial number of A molecules
    B0 100  # Initial number of B molecules
    ka 0.01 # A-B association rate constant (1/molecule 1/s)
    kd 1    # A-B dissociation rate constant (1/s)
  end parameters
  begin molecule types
    A(b,b)
    B(a)
  end molecule types
  begin seed species
    A(b,b) A0
    B(a) B0
  end seed species
  begin observables
    Molecules FreeAsites A(b)
    Molecules FreeB B(a)
    Molecules ABbonds A(b!1).B(a!1)
    Species BAB B(a!1).A(b!1,b!2).B(a!2)
  end observables
  begin reaction rules
    A(b) + B(a) <-> A(b!1).B(a!1) ka, kd
  end reaction rules
end model

simulate({method=>"ode",t_end=>10,n_steps=>200})
